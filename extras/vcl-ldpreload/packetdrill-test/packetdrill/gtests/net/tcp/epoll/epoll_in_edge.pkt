// Test the behavior of EPOLLET for EPOLLIN.
//
// If new data comes in, epoll_wait returns EPOLLIN regardless of whether
// we have drained the socket or not.
//`../common/defaults.sh`
--tolerance_usecs=500000

    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   +0 < S 0:0(0) win 32792 <mss 1000,sackOK,nop,nop,nop,wscale 7>
   +0 > S. 0:0(0) ack 1 win 4096 <mss 1460,wscale 10,sackOK,nop,nop,nop>
   +0 < . 1:1(0) ack 1 win 257

   +0 accept(3, ..., ...) = 4

   +0 fcntl(4, F_SETFL, O_RDWR|O_NONBLOCK) = 0
   +0 epoll_create(1) = 5

   // Add the FD as EPOLLET and clear all the events.
   +0 epoll_ctl(5, EPOLL_CTL_ADD, 4, {events=EPOLLOUT|EPOLLIN|EPOLLET, fd=4}) = 0
   +0 epoll_wait(5, {events=EPOLLOUT, fd=4}, 1, 0) = 1
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   // We have 1KB of data ready to read so we get EPOLLIN.
   +0 < P. 1:1001(1000) ack 1 win 257
   +0 > . 1:1(0) ack 1001

   // remove EPOLLOUT for VPP
   +0 epoll_wait(5, {events=EPOLLIN, fd=4}, 1, 0) = 1
   // Now epoll_wait should not return any event because there is no new data.
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   +0 read(4, ..., 1000) = 1000
   // We have 1KB of data ready to read so we get EPOLLIN.
   +.5 < P. 1001:2001(1000) ack 1 win 257
   +0 > . 1:1(0) ack 2001

   // Remove EPOLLOUT for VPP
   +.5 epoll_wait(5, {events=EPOLLIN, fd=4}, 1, 0) = 1
   // Now epoll_wait should not return any event because there is no new data.
   +0.5 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   // Read some data from the socket, but do not drain it.
   +0 read(4, ..., 500) = 500
   // epoll_wait should still return no events because there is no new data.
   +0.5 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0

   +.5 < P. 2001:3001(1000) ack 1 win 257
   +0 > . 1:1(0) ack 3001

   // VPP won't generate EPOLLIN because there are still some data that app need to read
   +0.5 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0
   // Now epoll_wait should not return any event because there is no new data.
   +0 epoll_wait(5, {events=0, ptr=0}, 1, 0) = 0
