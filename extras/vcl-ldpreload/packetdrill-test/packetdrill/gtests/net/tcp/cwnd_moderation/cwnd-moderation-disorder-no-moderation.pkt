// Test that if we do not moderate cwnd when observing out-of-order packets.

`../common/defaults.sh`

// Establish a connection.
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

  +.1 < S 0:0(0) win 32792 <mss 1000,sackOK,nop,nop,nop,wscale 7>
   +0 > S. 0:0(0) ack 1 <mss 1460,nop,nop,sackOK,nop,wscale 8>
// RTT 100ms
  +.1 < . 1:1(0) ack 1 win 320
   +0 accept(3, ..., ...) = 4

// Send 4 data segments.
   +0 write(4, ..., 4000) = 4000
   +0 > P. 1:4001(4000) ack 1
+.005 %{ assert tcpi_snd_cwnd == 10, tcpi_snd_cwnd }%

+.095 < . 1:1(0) ack 1 win 320 <sack 1001:2001,nop,nop>
+.005 %{
assert tcpi_ca_state == TCP_CA_Disorder, tcpi_ca_state
assert tcpi_snd_cwnd == 10, tcpi_snd_cwnd  # no cwnd moderation
}%
