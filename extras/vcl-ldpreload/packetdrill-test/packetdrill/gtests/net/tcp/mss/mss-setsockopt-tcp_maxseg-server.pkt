// Test TCP_MAXSEG works on passive/server TCP connections.

`../common/defaults.sh`

// Set up a listening socket.
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

// Set MSS to 1100.
 +.01 setsockopt(3, SOL_TCP, TCP_MAXSEG, [1100], 4) = 0
 +.01 getsockopt(3, SOL_TCP, TCP_MAXSEG, [536], [4]) = 0

// Establish a connection with an outgoing advertised MSS of 1100.
 +.08 < S 0:0(0) win 32792 <mss 1300,nop,wscale 7>
   +0 > S. 0:0(0) ack 1 <mss 1100,nop,wscale 8>
  +.1 < . 1:1(0) ack 1 win 257

  +.1 accept(3, ..., ...) = 4

  +.1 getsockopt(4, SOL_TCP, TCP_MAXSEG, [1100], [4]) = 0

  +.1 %{ assert tcpi_advmss == 1100, tcpi_advmss; assert tcpi_snd_mss == 1100, tcpi_snd_mss }%

// IW10 MSS should yield outgoing TSO packet with 10*1100 == 11000 bytes:
  +.1 write(4, ..., 12000) = 12000
   +0 > P. 1:11001(11000) ack 1
